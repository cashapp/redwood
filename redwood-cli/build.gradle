import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

apply plugin: 'java-library'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'application'

application {
  applicationName = 'redwood'
  mainClass.set('app.cash.redwood.cli.Main')
}

tasks.withType(JavaCompile).configureEach {
  sourceCompatibility = JavaVersion.VERSION_11.toString()
  targetCompatibility = JavaVersion.VERSION_11.toString()
}

tasks.withType(KotlinJvmCompile).configureEach {
  compilerOptions {
    jvmTarget.set(JvmTarget.JVM_11)
  }
}

dependencies {
  implementation projects.redwoodToolingCodegen
  implementation projects.redwoodToolingSchema
  implementation libs.lint.core
  implementation libs.clikt
}

tasks.named("distTar").configure { task ->
  task.enabled = false
}
tasks.named("assemble").configure { task ->
  task.dependsOn(tasks.named("installDist"))
}
