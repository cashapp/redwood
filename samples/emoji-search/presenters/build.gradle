apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'com.android.library'
apply plugin: 'app.cash.zipline'

kotlin {
  iosArm64()
  iosX64()
  iosSimulatorArm64()

  android()

  js {
    browser()
    binaries.executable()
  }

  sourceSets {
    commonMain {
      dependencies {
        api libs.zipline
      }
    }

    hostMain {
      dependsOn(commonMain)
      dependencies {
        implementation libs.zipline.loader
        api libs.okio
      }
    }

    androidMain {
      dependsOn(hostMain)
      dependencies {
        implementation libs.okHttp
      }
    }

    iosMain {
      dependsOn(hostMain)
    }
    targets.all { target ->
      // Some Kotlin targets do not have this property, but native ones always will.
      if (target.hasProperty('platformType') && target.platformType.name == "native") {
        target.compilations.main.source(sourceSets.iosMain)
      }
    }
  }
}

android {
  namespace = "app.cash.zipline.samples.emojisearch.presenters"
}

zipline {
  mainFunction = 'app.cash.zipline.samples.emojisearch.preparePresenters'
}

apply from: "${rootDir}/../dependOnAndroidTestSourceSets.gradle"
