apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'app.cash.zipline'

kotlin {
  iosArm64()
  iosX64()
  iosSimulatorArm64()

  jvm()

  js {
    browser()
    binaries.executable()
  }

  sourceSets {
    commonMain {
      dependencies {
        api projects.redwoodTreehouse
      }
    }

    jsMain {
      dependencies {
        api projects.redwoodTreehouseGuest
        api projects.samples.emojiSearch.presenter
        implementation projects.samples.emojiSearch.schema.compose.protocol
      }
    }

    iosMain {
      dependsOn(commonMain)
    }
    targets.all { target ->
      // Some Kotlin targets do not have this property, but native ones always will.
      if (target.hasProperty('platformType') && target.platformType.name == "native") {
        target.compilations.main.defaultSourceSet.dependsOn(sourceSets.iosMain)
      }
    }
  }
}

zipline {
  mainFunction = 'com.example.redwood.emojisearch.treehouse.preparePresenters'
}
