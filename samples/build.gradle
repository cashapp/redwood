buildscript {
  dependencies {
    classpath libs.kotlin.gradlePlugin
    classpath libs.kotlin.serializationPlugin
    classpath libs.androidGradlePlugin
    classpath libs.spotlessPlugin
    classpath libs.zipline.gradlePlugin

    // Normally you would declare a version here, but we use dependency substitution in
    // settings.gradle to use the version built from inside the repo.
    classpath 'app.cash.redwood:redwood-gradle-plugin'
  }
  repositories {
    mavenCentral()
    google()
  }
}

allprojects {
  plugins.withId('org.jetbrains.kotlin.multiplatform') {
    afterEvaluate {
      def compileMetadataTask = tasks.findByName("compileCommonMainKotlinMetadata")
      if (compileMetadataTask != null) {
        // https://youtrack.jetbrains.com/issue/KT-51293
        compileMetadataTask.enabled = false
      }
    }
  }

  repositories {
    mavenCentral()
    google()
  }

  tasks.withType(Test).configureEach {
    testLogging {
      if (System.getenv("CI") == "true") {
        events = ["failed", "skipped", "passed"]
      }
      exceptionFormat "full"
    }
  }

  plugins.withId('java-base') {
    java {
      sourceCompatibility = JavaVersion.VERSION_1_8
      targetCompatibility = JavaVersion.VERSION_1_8
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
    task.kotlinOptions {
      jvmTarget = '1.8'
    }
  }

  plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
    def android = plugin.extension
    android.compileSdkVersion 32
    android.compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }
    android.defaultConfig {
      minSdkVersion 21
      targetSdkVersion 30
    }
    android.lintOptions {
      checkDependencies true
      checkReleaseBuilds false // Full lint runs as part of 'build' task.
    }
    android.composeOptions {
      kotlinCompilerExtensionVersion = libs.versions.androidxComposeCompiler.get()
    }

    // TODO Re-enable lint on samples https://issuetracker.google.com/issues/196982350
    afterEvaluate {
      tasks.named('lintReportDebug').configure { enabled = false }
      tasks.named('lintDebug').configure { enabled = false }
      tasks.named('lintReportRelease').configure { enabled = false }
      tasks.named('lintRelease').configure { enabled = false }
    }
  }

  apply plugin: 'com.diffplug.spotless'

  spotless {
    kotlin {
      target("src/*/kotlin/**/*.kt")
      licenseHeaderFile(rootProject.file('../gradle/license-header.txt'))
      // Spotless doesn't read .editorconfig yet: https://github.com/diffplug/spotless/issues/142
      ktlint('0.46.1').editorConfigOverride([
        'disabled_rules': 'filename',
        'insert_final_newline': 'true',
        'end_of_line': 'lf',
        'charset': 'utf-8',
        'indent_size': '2',
        'ij_kotlin_allow_trailing_comma': 'true',
        'ij_kotlin_allow_trailing_comma_on_call_site': 'true',
        'ij_kotlin_imports_layout': '*',
      ])
    }
  }
}
