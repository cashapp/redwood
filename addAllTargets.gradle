plugins.withId('org.jetbrains.kotlin.multiplatform') {
  kotlin {
    if (plugins.hasPlugin('com.android.library')) {
      android {
        publishLibraryVariants('release')
      }
    }

    iosArm64()
    iosX64()
    iosSimulatorArm64()

    js {
      browser()
    }

    jvm()

    macosArm64()
    macosX64()

    sourceSets {
      nativeMain {
        dependsOn(commonMain)
      }
      nativeTest {
        dependsOn(commonTest)
      }

      iosMain {
        dependsOn(nativeMain)
      }
      iosTest {
        dependsOn(nativeTest)
      }

      macosMain {
        dependsOn(nativeMain)
      }
      macosTest {
        dependsOn(nativeTest)
      }
    }

    targets.all { target ->
      // Some Kotlin targets do not have this property, but native ones always will.
      if (target.hasProperty('platformType') && target.platformType.name == "native") {
        if (target.name.startsWith('ios')) {
          target.compilations.main.defaultSourceSet.dependsOn(sourceSets.iosMain)
          target.compilations.test.defaultSourceSet.dependsOn(sourceSets.iosTest)
        } else if (target.name.startsWith('macos')) {
          target.compilations.main.defaultSourceSet.dependsOn(sourceSets.macosMain)
          target.compilations.test.defaultSourceSet.dependsOn(sourceSets.macosTest)
        } else {
          throw new AssertionError("Unknown target ${target.name}")
        }
      }
    }
  }
}
