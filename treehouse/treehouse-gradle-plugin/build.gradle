apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java-gradle-plugin'
apply plugin: 'app.cash.exhaustive'
apply plugin: 'com.vanniktech.maven.publish'

dependencies {
  compileOnly gradleApi()
  compileOnly deps.kotlin.gradlePlugin

  testImplementation deps.junit
  testImplementation deps.testParameterInjector
  testImplementation deps.truth
  testImplementation gradleTestKit()
}

gradlePlugin {
  plugins {
    treehouse {
      id = "app.cash.treehouse"
      displayName = "Treehouse"
      description = "Treehouse client Gradle plugin"
      implementationClass = "app.cash.treehouse.gradle.TreehousePlugin"
    }
    treehouseSchema {
      id = "app.cash.treehouse.schema"
      displayName = "Treehouse schema"
      description = "Treehouse schema Gradle plugin"
      implementationClass = "app.cash.treehouse.gradle.TreehouseSchemaPlugin"
    }
    treehouseSchemaCompose {
      id = "app.cash.treehouse.schema.compose"
      displayName = "Treehouse schema (Compose generator)"
      description = "Treehouse schema Compose code generation Gradle plugin"
      implementationClass = "app.cash.treehouse.gradle.TreehouseSchemaComposePlugin"
    }
    treehouseSchemaTest {
      id = "app.cash.treehouse.schema.test"
      displayName = "Treehouse schema (test generator)"
      description = "Treehouse schema test code generation Gradle plugin"
      implementationClass = "app.cash.treehouse.gradle.TreehouseSchemaTestPlugin"
    }
    treehouseSchemaWidget {
      id = "app.cash.treehouse.schema.widget"
      displayName = "Treehouse schema (widget generator)"
      description = "Treehouse schema widget code generation Gradle plugin"
      implementationClass = "app.cash.treehouse.gradle.TreehouseSchemaWidgetPlugin"
    }
  }
}

test {
  dependsOn(':compose:compiler:installArchives')
  dependsOn(':compose:runtime:installArchives')
  dependsOn(':treehouse-compose:installArchives')
  dependsOn(':treehouse-gradle-plugin:installArchives')
  dependsOn(':treehouse-protocol:installArchives')
  dependsOn(':treehouse-schema:installArchives')
  dependsOn(':treehouse-schema-generator:installArchives')
  dependsOn(':treehouse-schema-annotations:installArchives')
  dependsOn(':treehouse-widget:installArchives')
}

def versionDirectory = "$buildDir/generated/version/"

sourceSets {
  main.java.srcDir versionDirectory
}

task pluginVersion {
  def outputDir = file(versionDirectory)

  inputs.property 'treehouseVersion', project.version
  outputs.dir outputDir

  doLast {
    def versionFile = file("$outputDir/app/cash/treehouse/gradle/version.kt")
    versionFile.parentFile.mkdirs()
    versionFile.text = """// Generated file. Do not edit!
package app.cash.treehouse.gradle

internal const val treehouseVersion = "${project.version}"
"""
  }
}

tasks.getByName('compileKotlin').dependsOn('pluginVersion')
