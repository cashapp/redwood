import org.jetbrains.kotlin.gradle.plugin.KotlinPluginKt

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.

kotlin {
  android {
    publishLibraryVariants('release')
  }
  iosArm32()
  iosArm64()
  iosX64()
  js {
    browser()
  }
  jvm()
  macosX64()
  mingwX64()
  tvosX64()
  tvosArm64()
  watchosArm32()
  watchosArm64()

  sourceSets {
    commonMain {
      dependencies {
        api deps.kotlinx.coroutines.core
        api project(':treehouse-protocol')
        api project(':compose:runtime')
      }
    }
    commonTest {
      dependencies {
        implementation deps.kotlin.test
        implementation project(':treehouse-test-schema:compose')
      }
    }
    androidMain {
      dependencies {
        implementation deps.androidx.core
      }
    }
    androidTest {
      dependsOn jvmTest
    }
    nativeTest {
    }
  }

  configure([
    targets.iosArm32,
    targets.iosArm64,
    targets.iosX64,
    targets.macosX64,
    targets.mingwX64,
    targets.tvosArm64,
    targets.tvosX64,
    targets.watchosArm32,
    targets.watchosArm64,
  ]) {
    compilations.test.source(sourceSets.nativeTest)
  }
}

android {
  testOptions {
    unitTests.returnDefaultValues = true
  }
}

dependencies {
  add(KotlinPluginKt.PLUGIN_CLASSPATH_CONFIGURATION_NAME, deps.androidx.compose.compiler)
  add(KotlinPluginKt.NATIVE_COMPILER_PLUGIN_CLASSPATH_CONFIGURATION_NAME, project(':compose:compiler-hosted'))
}

spotless {
  kotlin {
    targetExclude(
      // Apache 2-licensed files from AOSP.
      "src/androidMain/kotlin/app/cash/treehouse/compose/AndroidUiDispatcher.kt",
      "src/androidMain/kotlin/app/cash/treehouse/compose/AndroidUiFrameClock.kt",
    )
  }
}
