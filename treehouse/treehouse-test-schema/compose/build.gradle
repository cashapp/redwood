import org.jetbrains.kotlin.gradle.plugin.KotlinPluginKt

apply plugin: 'org.jetbrains.kotlin.multiplatform'

def generatedDir = 'build/generated/treehouse'

kotlin {
  apply from: "${rootDir}/addAllTargets.gradle"

  sourceSets {
    commonMain {
      kotlin {
        srcDir generatedDir
      }
      dependencies {
        api project(':treehouse-test-schema:widget')
        api project(':treehouse-compose')
      }
    }
  }
}

configurations {
  generator
}

dependencies {
  add(KotlinPluginKt.PLUGIN_CLASSPATH_CONFIGURATION_NAME, deps.androidx.compose.compiler)
  add(KotlinPluginKt.NATIVE_COMPILER_PLUGIN_CLASSPATH_CONFIGURATION_NAME, project(':compose:compiler-hosted'))

  generator project(':treehouse-schema-generator')
  generator project(':treehouse-test-schema')
}

def generate = tasks.register('treehouseGenerate', JavaExec) {
  it.classpath(configurations.generator)
  it.main('app.cash.treehouse.schema.generator.Main')
  it.args(
    '--compose',
    '--out', generatedDir,
    'example.treehouse.ExampleSchema',
  )

  it.doFirst {
    file(generatedDir).deleteDir()
  }
}

kotlin.targets.all { target ->
  target.compilations.all { compilation ->
    compilation.compileKotlinTask.dependsOn(generate.get())
  }
}
