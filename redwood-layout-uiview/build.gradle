import app.cash.redwood.buildsupport.ComposeHelpers
import app.cash.redwood.buildsupport.FlexboxHelpers

apply plugin: 'org.jetbrains.kotlin.multiplatform'

redwoodBuild {
  publishing()
}

kotlin {
  [
    iosArm64(),
    iosX64(),
    iosSimulatorArm64(),
  ].each { iosTarget ->
    iosTarget.binaries.framework {
      compilation = iosTarget.compilations.test
      baseName = 'RedwoodLayoutUIViewTestKt'
    }
  }

  sourceSets {
    commonMain {
      kotlin.srcDir(ComposeHelpers.get(tasks, 'app.cash.redwood.layout.uiview'))
      kotlin.srcDir(FlexboxHelpers.get(tasks, 'app.cash.redwood.layout.uiview'))
      dependencies {
        api projects.redwoodLayoutWidget
        implementation projects.redwoodWidgetCompose
        implementation projects.redwoodYoga
      }
    }
    commonTest {
      dependencies {
        api projects.redwoodLayoutSharedTest
        api libs.kotlin.test
      }
    }
  }
}

spotless {
  kotlin {
    targetExclude(
      // MIT-licensed files from Meta.
      "src/commonMain/kotlin/app/cash/redwood/layout/uiview/YogaUIView.kt",
    )
  }
}
