import app.cash.redwood.buildsupport.FlexboxHelpers
import co.touchlab.cklib.gradle.CompileToBitcode

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.
apply plugin: 'co.touchlab.cklib'

kotlin {
  [
    iosArm64(),
    iosX64(),
    iosSimulatorArm64()
  ].forEach { target ->
    target.compilations.main.cinterops.create("uikit") {
      includeDirs("$projectDir/native/uikit")
    }
  }

  sourceSets {
    commonMain {
      kotlin.srcDir(FlexboxHelpers.get(tasks, 'app.cash.redwood.layout.uiview'))
      dependencies {
        api projects.redwoodLayoutWidget
        implementation projects.redwoodFlexbox
        implementation projects.redwoodWidgetCompose
        implementation projects.redwoodYoga
      }
    }

    iosArm64Main {
      dependsOn commonMain
    }

    iosX64Main {
      dependsOn commonMain
    }

    iosSimulatorArm64Main {
      dependsOn commonMain
    }
  }
}

cklib {
  config.kotlinVersion = libs.versions.kotlin.get()
  create("uikit", file("native/uikit"), ["main"]) {ctb ->
    ctb.language = CompileToBitcode.Language.OBJC
    ctb.compilerArgs.addAll(
      ["-DKONAN_MI_MALLOC=1", "-DNS_FORMAT_ARGUMENT(A)=", "-D_Nullable_result=_Nullable"]
    )
    ctb.srcDirs = project.files(file("native/uikit"))
  }
}
