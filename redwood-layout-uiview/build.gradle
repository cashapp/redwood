import app.cash.redwood.buildsupport.ComposeHelpers
import app.cash.redwood.buildsupport.FlexboxHelpers

import static app.cash.redwood.buildsupport.TargetGroup.ToolkitIos
import static org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType.common

redwoodBuild {
  targets(ToolkitIos)
  publishing()
}

kotlin {
  targets.configureEach { target ->
    if (target.platformType != common) {
      target.binaries.framework {
        compilation = target.compilations.test
        baseName = 'RedwoodLayoutUIViewTestKt'
      }
    }
  }

  sourceSets {
    commonMain {
      kotlin.srcDir(ComposeHelpers.get(tasks, 'app.cash.redwood.layout.uiview'))
      kotlin.srcDir(FlexboxHelpers.get(tasks, 'app.cash.redwood.layout.uiview'))
      dependencies {
        api projects.redwoodLayoutWidget
        implementation projects.redwoodWidgetCompose
        implementation projects.redwoodYoga
      }
    }
    commonTest {
      dependencies {
        api projects.redwoodLayoutSharedTest
        api libs.kotlin.test
      }
    }
  }
}

spotless {
  kotlin {
    targetExclude(
      // MIT-licensed files from Meta.
      "src/commonMain/kotlin/app/cash/redwood/layout/uiview/YogaUIView.kt",
    )
  }
}
