import static app.cash.redwood.buildsupport.TargetGroup.ToolkitComposeUi

redwoodBuild {
  targets(ToolkitComposeUi)
  publishing()
}

apply plugin: 'org.jetbrains.kotlin.plugin.compose'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'app.cash.paparazzi'

kotlin {
  sourceSets {
    commonMain {
      kotlin.srcDir(redwoodBuild.generateFlexboxHelpers(tasks, 'app.cash.redwood.layout.composeui'))
      dependencies {
        api projects.redwoodLayoutWidget
        implementation projects.redwoodWidgetCompose
        implementation projects.redwoodYoga
        implementation libs.jetbrains.compose.foundation
      }
    }

    androidUnitTest {
      dependencies {
        implementation projects.redwoodLayoutSharedTest
        implementation libs.testParameterInjector
      }
    }
  }
}

android {
  namespace 'app.cash.redwood.layout.composeui'

  buildFeatures {
    // For Paparazzi, which internally uses resources to set itself up.
    androidResources = true
  }
}

afterEvaluate {
  spotless {
    kotlin {
      targetExclude(
        // Apache 2-licensed file from AOSP.
        "src/commonMain/kotlin/app/cash/redwood/layout/composeui/Box.kt",
      )
    }
  }
}
