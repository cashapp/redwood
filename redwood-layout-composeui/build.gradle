import app.cash.redwood.buildsupport.FlexboxHelpers

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'app.cash.paparazzi'

redwoodBuild {
  composeCompiler()
  publishing()
}

kotlin {
  android {
    publishLibraryVariants('release')
  }

  iosArm64()
  iosX64()
  iosSimulatorArm64()

  jvm()

  macosArm64()
  macosX64()

  sourceSets {
    commonMain {
      kotlin.srcDir(FlexboxHelpers.get(tasks, 'app.cash.redwood.layout.composeui').get())
      dependencies {
        api projects.redwoodLayoutWidget
        implementation projects.redwoodFlexbox
        implementation projects.redwoodWidgetCompose
        implementation libs.jetbrains.compose.foundation
      }
    }

    androidUnitTest {
      dependencies {
        implementation projects.redwoodLayoutSharedTest
      }
    }
  }
}

android {
  namespace 'app.cash.redwood.layout.composeui'
}
