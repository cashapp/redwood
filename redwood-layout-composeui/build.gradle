import app.cash.redwood.buildsupport.FlexboxHelpers
import app.cash.redwood.buildsupport.KmpTargets

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'app.cash.paparazzi'

redwoodBuild {
  composeCompiler()
  publishing()
}

kotlin {
  KmpTargets.addAllTargets(project, true /* skipJs */)

  sourceSets {
    commonMain {
      kotlin.srcDir(FlexboxHelpers.get(tasks, 'app.cash.redwood.layout.composeui').get())
      dependencies {
        api projects.redwoodLayoutWidget
        implementation projects.redwoodWidgetCompose
        implementation projects.redwoodYoga
        implementation libs.jetbrains.compose.foundation
      }
    }

    androidUnitTest {
      dependencies {
        implementation projects.redwoodLayoutSharedTest
        implementation libs.testParameterInjector
      }
    }
  }
}

android {
  namespace 'app.cash.redwood.layout.composeui'

  buildFeatures {
    // For Paparazzi, which internally uses resources to set itself up.
    androidResources = true
  }
}

afterEvaluate {
  spotless {
    kotlin {
      targetExclude(
        // Apache 2-licensed file from AOSP.
        "src/commonMain/kotlin/app/cash/redwood/layout/composeui/Box.kt",
      )
    }
  }
}
