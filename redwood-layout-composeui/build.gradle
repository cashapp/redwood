import app.cash.redwood.buildsupport.FlexboxHelpers

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'app.cash.paparazzi'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.

dependencies {
  api projects.redwoodLayoutWidget
  implementation projects.redwoodFlexbox
  implementation projects.redwoodWidgetCompose
  implementation libs.androidx.core
  implementation libs.androidx.compose.foundation
  testImplementation projects.redwoodLayoutSharedTest
}

android {
  namespace 'app.cash.redwood.layout.composeui'

  buildFeatures {
    compose = true
  }

  def helpers = FlexboxHelpers.get(tasks, 'app.cash.redwood.layout.composeui').get()
  // TODO Should be able to do `sourceSets.main.kotlin.srcDir(helpers)`.
  //  https://issuetracker.google.com/issues/249468390
  libraryVariants.all { variant ->
    variant.registerJavaGeneratingTask(helpers, helpers.outputDirectory.get().asFile)
  }
}
