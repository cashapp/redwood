import app.cash.redwood.buildsupport.ComposeHelpers
import app.cash.redwood.buildsupport.FlexboxHelpers

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'app.cash.paparazzi'

redwoodBuild {
  publishing()
}

dependencies {
  api projects.redwoodLayoutWidget

  implementation libs.androidx.core
  implementation projects.redwoodYoga

  testImplementation libs.testParameterInjector
  testImplementation projects.redwoodLayoutSharedTest
}

android {
  namespace 'app.cash.redwood.layout.view'

  def composeHelpers = ComposeHelpers.get(tasks, 'app.cash.redwood.layout.view').get()
  def flexboxHelpers = FlexboxHelpers.get(tasks, 'app.cash.redwood.layout.view').get()
  // TODO Should be able to do `sourceSets.main.kotlin.srcDir(helpers)`.
  //  https://issuetracker.google.com/issues/249468390
  libraryVariants.all { variant ->
    variant.registerJavaGeneratingTask(composeHelpers, composeHelpers.outputDirectory.get().asFile)
    variant.registerJavaGeneratingTask(flexboxHelpers, flexboxHelpers.outputDirectory.get().asFile)
  }

  buildFeatures {
    // TODO Remove once https://github.com/cashapp/paparazzi/issues/472 fixed.
    androidResources = true
  }
}

spotless {
  kotlin {
    targetExclude(
      // MIT-licensed files from Meta.
      "src/main/kotlin/app/cash/redwood/layout/view/YogaLayout.kt",
    )
  }
}
