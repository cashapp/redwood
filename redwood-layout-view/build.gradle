import static app.cash.redwood.buildsupport.TargetGroup.ToolkitAndroid

redwoodBuild {
  targets(ToolkitAndroid)
  publishing()
}

apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'app.cash.burst'
apply plugin: 'app.cash.paparazzi'

dependencies {
  api projects.redwoodLayoutWidget
  implementation projects.redwoodYoga
  implementation libs.androidx.core
  testImplementation libs.burst
  testImplementation projects.redwoodLayoutSharedTest
  testImplementation projects.redwoodSnapshotTesting
}

android {
  namespace 'app.cash.redwood.layout.view'

  def composeHelpers = redwoodBuild.generateComposeHelpers(tasks, 'app.cash.redwood.layout.view').get()
  def flexboxHelpers = redwoodBuild.generateFlexboxHelpers(tasks, 'app.cash.redwood.layout.view').get()
  // TODO Should be able to do `sourceSets.main.kotlin.srcDir(helpers)`.
  //  https://issuetracker.google.com/issues/249468390
  libraryVariants.all { variant ->
    variant.registerJavaGeneratingTask(composeHelpers, composeHelpers.outputDirectory.get().asFile)
    variant.registerJavaGeneratingTask(flexboxHelpers, flexboxHelpers.outputDirectory.get().asFile)
  }

  buildFeatures {
    // For Paparazzi, which internally uses resources to set itself up.
    androidResources = true
  }
}

spotless {
  kotlin {
    targetExclude(
      // MIT-licensed files from Meta.
      "src/main/kotlin/app/cash/redwood/layout/view/YogaLayout.kt",
    )
  }
}
