import app.cash.redwood.buildsupport.FlexboxLayoutGenerateTask

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'app.cash.paparazzi'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.

dependencies {
  api projects.redwoodLayoutSchema.widget
  implementation projects.redwoodFlexContainer
  implementation libs.androidx.core
  testImplementation projects.redwoodLayoutSharedTest
}

def flexboxLayout = tasks.register("flexboxLayout", FlexboxLayoutGenerateTask) {
  packageName = 'app.cash.redwood.layout.view'
}

android {
  namespace 'app.cash.redwood.layout.view'

  // TODO Should be able to do `sourceSets.main.kotlin.srcDir(flexboxLayout)`.
  //  https://issuetracker.google.com/issues/249468390
  libraryVariants.all { variant ->
    def task = flexboxLayout.get()
    variant.registerJavaGeneratingTask(task, task.outputDirectory.get().asFile)
  }
}
