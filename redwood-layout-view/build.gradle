import app.cash.redwood.buildsupport.FlexboxHelpers

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'app.cash.paparazzi'

redwoodBuild {
  publishing()
}

dependencies {
  api projects.redwoodLayoutWidget
  implementation projects.redwoodYoga
  implementation libs.androidx.core
  testImplementation projects.redwoodLayoutSharedTest
}

android {
  namespace 'app.cash.redwood.layout.view'

  def helpers = FlexboxHelpers.get(tasks, 'app.cash.redwood.layout.view').get()
  // TODO Should be able to do `sourceSets.main.kotlin.srcDir(helpers)`.
  //  https://issuetracker.google.com/issues/249468390
  libraryVariants.all { variant ->
    variant.registerJavaGeneratingTask(helpers, helpers.outputDirectory.get().asFile)
  }
}

spotless {
  kotlin {
    targetExclude(
      // MIT-licensed files from Meta.
      "src/main/kotlin/app/cash/redwood/layout/view/YogaLayout.kt",
    )
  }
}
