buildscript {
  dependencies {
    classpath libs.kotlin.gradlePlugin
    classpath libs.buildConfigPlugin
  }
  repositories {
    mavenCentral(); mavenLocal(); maven { url "https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    gradlePluginPortal()
  }
}

allprojects {
  repositories {
    mavenCentral(); mavenLocal(); maven { url "https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    google()
  }
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.github.gmazzo.buildconfig'

dependencies {
  compileOnly gradleApi()
  compileOnly libs.kotlin.gradlePlugin
}

gradlePlugin {
  plugins {
    jetbrainsCompose {
      id = "app.cash.redwood.build.compose"
      displayName = "JetBrains Compose"
      description = "Gradle plugin to apply JetBrains Compose compiler"
      implementationClass = "app.cash.redwood.buildsupport.ComposePlugin"
    }
  }
}

buildConfig {
  useKotlinOutput {
    internalVisibility = true
    topLevelConstants = true
  }

  packageName('app.cash.redwood.buildsupport')
  buildConfigField("String", "jbComposeCompiler", "\"${libs.versions.jbComposeCompiler.get()}\"")
}
