import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

buildscript {
  dependencies {
    classpath libs.kotlin.gradlePlugin
    classpath libs.buildConfigPlugin
  }
  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
  }
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.github.gmazzo.buildconfig'

dependencies {
  compileOnly gradleApi()
  compileOnly libs.kotlin.gradlePlugin
  implementation libs.gradleMavenPublishPlugin
  // implementation libs.dokkaPlugin
  implementation libs.spotlessPlugin

  // Expose the generated version catalog API to the plugin.
  implementation(files(libs.class.superclass.protectionDomain.codeSource.location))
}

gradlePlugin {
  plugins {
    redwoodBuild {
      id = "app.cash.redwood.build"
      displayName = "Redwood Build plugin"
      description = "Gradle plugin for Redwood build things"
      implementationClass = "app.cash.redwood.buildsupport.RedwoodBuildPlugin"
    }
  }
}

buildConfig {
  useKotlinOutput {
    internalVisibility = true
    topLevelConstants = true
  }

  packageName('app.cash.redwood.buildsupport')
  buildConfigField("String", "composeCompilerGroupId", "\"${libs.jetbrains.compose.compiler.get().module.group}\"")
  buildConfigField("String", "composeCompilerArtifactId", "\"${libs.jetbrains.compose.compiler.get().module.name}\"")
  buildConfigField("String", "composeCompilerVersion", "\"${libs.jetbrains.compose.compiler.get().version}\"")
}

tasks.withType(JavaCompile).configureEach {
  sourceCompatibility = JavaVersion.VERSION_11.toString()
  targetCompatibility = JavaVersion.VERSION_11.toString()
}

tasks.withType(KotlinJvmCompile).configureEach {
  compilerOptions {
    jvmTarget.set(JvmTarget.JVM_11)
  }
}
