import app.cash.redwood.buildsupport.ComposeHelpersGenerateTask

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.

def composeHelpers = tasks.register("composeHelpers", ComposeHelpersGenerateTask) {
  packageName = 'app.cash.redwood.protocol.widget'
}

kotlin {
  apply from: "${rootDir}/addAllTargets.gradle"

  sourceSets {
    commonMain {
      kotlin.srcDir(composeHelpers)
      dependencies {
        api projects.redwoodProtocol
        api projects.redwoodWidget
      }
    }
    commonTest {
      dependencies {
        implementation projects.testSchema.compose
        implementation projects.testSchema.widget.protocol
        implementation libs.kotlin.test
      }
    }
  }
}

android {
  namespace 'app.cash.redwood.protocol.widget'
}

spotless {
  kotlin {
    targetExclude(
      // Apache 2-licensed files from AOSP.
      "src/commonMain/kotlin/app/cash/redwood/protocol/widget/utils.kt",
    )
  }
}
