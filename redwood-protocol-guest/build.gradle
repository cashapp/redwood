import app.cash.redwood.buildsupport.ComposeHelpers

import static app.cash.redwood.buildsupport.TargetGroup.Common

redwoodBuild {
  targets(Common)
  composeCompiler()
  publishing()
}

apply plugin: 'com.github.gmazzo.buildconfig'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'

kotlin {
  sourceSets {
    commonMain {
      kotlin.srcDir(ComposeHelpers.get(tasks, 'app.cash.redwood.protocol.guest'))
      dependencies {
        api projects.redwoodProtocol
        api projects.redwoodCompose
      }
    }
    commonTest {
      dependencies {
        implementation libs.kotlin.test
        implementation libs.assertk
        implementation libs.kotlinx.coroutines.test
        implementation projects.redwoodLazylayoutCompose
        implementation projects.redwoodTesting
        implementation projects.testApp.schema.compose
        implementation projects.testApp.schema.protocolGuest
      }
    }
    nonJsMain {
      dependsOn(commonMain)
    }
    jvmMain {
      dependsOn(nonJsMain)
    }
    nativeMain {
      dependsOn(nonJsMain)
    }
  }
}

buildConfig {
  useKotlinOutput {
    topLevelConstants = true
  }

  className("Version")
  packageName('app.cash.redwood.protocol.guest')
  buildConfigField(
    "app.cash.redwood.protocol.RedwoodVersion",
    "guestRedwoodVersion",
    "RedwoodVersion(\"${project.version}\")",
  )
}
