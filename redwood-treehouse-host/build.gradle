import app.cash.redwood.buildsupport.KmpTargets

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'com.android.library'
apply plugin: 'app.cash.zipline'

redwoodBuild {
  publishing()
}

kotlin {
  KmpTargets.addAllTargets(project, true /* skipJs */)

  sourceSets {
    commonMain {
      dependencies {
        api libs.kotlinx.coroutines.core
        api libs.kotlinx.serialization.json
        api libs.okio
        api libs.zipline
        api libs.zipline.loader
        api projects.redwoodProtocol
        api projects.redwoodProtocolHost
        api projects.redwoodTreehouse
      }
    }

    androidMain {
      dependencies {
        api libs.okhttp
        implementation libs.androidx.activity
      }
    }

    commonTest {
      dependencies {
        implementation libs.assertk
        implementation libs.kotlin.test
        implementation libs.kotlinx.coroutines.test
        implementation libs.okio
        implementation libs.turbine
        implementation projects.redwoodLayoutTesting
        implementation projects.redwoodLazylayoutTesting
        implementation projects.testApp.schema.protocolHost
        implementation projects.testApp.schema.testing
      }
      if (!rootProject.hasProperty('redwoodNoApps')) {
        kotlin.srcDir('src/appsTest/kotlin')
        dependencies {
          implementation projects.testApp.presenterTreehouse
        }
      }
    }
    androidUnitTest {
      dependsOn(commonTest)
      dependencies {
        implementation libs.robolectric
      }
    }
    jvmTest {
      if (!rootProject.hasProperty('redwoodNoApps')) {
        kotlin.srcDir('src/appsJvmTest/kotlin')
      }
    }
  }
}

dependencies {
  androidTestImplementation libs.junit
  androidTestImplementation libs.turbine
  androidTestImplementation libs.androidx.test.runner
  androidTestImplementation libs.kotlinx.coroutines.test
  androidTestImplementation libs.assertk
  androidTestImplementation projects.redwoodLayoutTesting
  androidTestImplementation projects.redwoodLazylayoutTesting
  androidTestImplementation projects.testApp.schema.protocolHost
  androidTestImplementation projects.testApp.schema.testing

  // The kotlin.test library provides JVM variants for multiple testing frameworks. The Kotlin
  // plugin does not apply the capability to configurations created by the Android plugin.
  androidTestImplementation libs.kotlin.test.junit
}

android {
  namespace 'app.cash.treehouse.host'

  sourceSets {
    androidTest {
      java.srcDir('src/commonTest/kotlin')
    }
  }

  defaultConfig {
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
  }

  buildFeatures {
    // Needed for unit tests which use ViewCompat whose static fields reference `R.id`s.
    androidResources = true
  }
}

// TreehouseTester expects to load our test application.
if (!rootProject.hasProperty('redwoodNoApps')) {
  tasks.withType(Test.class).configureEach {
    it.dependsOn(":test-app:presenter-treehouse:compileDevelopmentExecutableKotlinJsZipline")
  }
}
