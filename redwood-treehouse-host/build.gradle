import app.cash.redwood.buildsupport.KmpTargets

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'com.android.library'
apply plugin: 'app.cash.zipline'

redwoodBuild {
  publishing()
}

kotlin {
  KmpTargets.addAllTargets(project, true /* skipJs */)

  sourceSets {
    commonMain {
      dependencies {
        api libs.kotlinx.coroutines.core
        api libs.kotlinx.serialization.json
        api libs.okio
        api libs.zipline
        api libs.zipline.loader
        api projects.redwoodProtocol
        api projects.redwoodProtocolWidget
        api projects.redwoodTreehouse
      }
    }

    androidMain {
      dependencies {
        api libs.okHttp
        implementation libs.androidx.core
      }
    }

    commonTest {
      dependencies {
        implementation libs.kotlin.test
        implementation libs.assertk
        implementation libs.kotlinx.coroutines.test
        implementation libs.turbine
      }
    }
    androidUnitTest {
      dependsOn(commonTest)
      dependencies {
        implementation libs.robolectric
      }
    }
  }
}

dependencies {
  androidTestImplementation libs.junit
  androidTestImplementation libs.turbine
  androidTestImplementation libs.androidx.test.runner
  androidTestImplementation libs.kotlinx.coroutines.test
  androidTestImplementation libs.assertk

  // The kotlin.test library provides JVM variants for multiple testing frameworks. The Kotlin
  // plugin does not apply the capability to configurations created by the Android plugin.
  androidTestImplementation(libs.kotlin.test) {
    capabilities {
      requireCapability(
        "org.jetbrains.kotlin:kotlin-test-framework-junit:${libs.versions.kotlin.get()}")
    }
  }
}

android {
  namespace 'app.cash.treehouse.host'

  sourceSets {
    androidTest {
      java.srcDir('src/commonTest/kotlin')
    }
  }

  defaultConfig {
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
  }

  buildFeatures {
    // Needed for unit tests which use ViewCompat whose static fields reference `R.id`s.
    androidResources = true
  }
}
