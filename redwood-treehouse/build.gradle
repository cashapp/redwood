apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.
apply plugin: 'app.cash.zipline'
apply plugin: 'app.cash.redwood.build.compose'

redwoodBuildCompose {
  targetCompilations = ['jsMain']
}

kotlin {
  apply from: "${rootDir}/addAllTargets.gradle"

  sourceSets {
    commonMain {
      dependencies {
        api projects.redwoodProtocol
        api libs.zipline
      }
    }

    jsMain {
      dependencies {
        api projects.redwoodProtocolCompose
        api libs.kotlinx.coroutines.core
        api libs.jetbrains.compose.runtime
      }
    }

    hostMain {
      dependsOn(commonMain)
      dependencies {
        api libs.zipline.loader
        api libs.kotlinx.coroutines.core
        api libs.kotlinx.serialization.json
        api libs.okio
        api projects.redwoodProtocolWidget
      }
    }
    nativeMain {
      dependsOn(hostMain)
    }
    commonJvmMain {
      dependsOn(hostMain)
      dependencies {
        api libs.okHttp
      }
    }
    androidMain {
      dependsOn(commonJvmMain)
    }
    jvmMain {
      dependsOn(commonJvmMain)
    }

    hostTest {
      dependencies {
        implementation libs.kotlin.test
        implementation libs.kotlinx.coroutines.test
        implementation libs.turbine
      }
    }
    nativeTest {
      dependsOn(hostTest)
    }
    commonJvmTest {
      dependsOn(hostTest)
    }
    androidTest {
      dependsOn(commonJvmTest)
      dependencies {
        implementation libs.robolectric
      }
    }
    jvmTest {
      dependsOn(commonJvmTest)
    }
  }
}

android {
  namespace 'app.cash.treehouse'
}
