import app.cash.redwood.buildsupport.ComposeHelpers
import app.cash.redwood.buildsupport.KmpTargets

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.
apply plugin: 'app.cash.redwood.build.compose'

kotlin {
  KmpTargets.addAllTargets(project)

  sourceSets {
    commonMain {
      kotlin.srcDir(ComposeHelpers.get(tasks, 'app.cash.redwood.protocol.compose'))
      dependencies {
        api projects.redwoodProtocol
        api projects.redwoodCompose
      }
    }
    commonTest {
      dependencies {
        implementation libs.kotlin.test
        implementation libs.assertk
        implementation libs.kotlinx.coroutines.test
        implementation projects.testSchema.compose
        implementation projects.testSchema.compose.protocol
      }
    }
    nonJsMain {
      dependsOn(commonMain)
    }
    androidMain {
      dependsOn(nonJsMain)
    }
    jvmMain {
      dependsOn(nonJsMain)
    }
    nativeMain {
      dependsOn(nonJsMain)
    }
  }
}

android {
  namespace 'app.cash.redwood.protocol.compose'

  testOptions {
    unitTests.returnDefaultValues = true
  }
}

spotless {
  kotlin {
    targetExclude(
      // Apache 2-licensed files from AOSP.
      "src/commonMain/kotlin/app/cash/redwood/protocol/compose/utils.kt",
    )
  }
}
