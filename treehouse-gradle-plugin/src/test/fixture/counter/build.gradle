buildscript {
  apply from: "../../../../../gradle/dependencies.gradle"

  dependencies {
    classpath "app.cash.treehouse:treehouse-gradle-plugin:$treehouseVersion"
    classpath deps.kotlin.gradlePlugin
    classpath deps.androidGradlePlugin
  }

  repositories {
    maven {
      url "file://${rootDir.absolutePath}/../../../../../build/localMaven"
    }
    mavenCentral()
    google()
    jcenter()
  }
}

allprojects {
  repositories {
    maven {
      url "file://${rootDir.absolutePath}/../../../../../build/localMaven"
    }
    mavenCentral()
    google()
    jcenter()
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
    task.kotlinOptions {
      freeCompilerArgs += [
        '-Xopt-in=kotlin.RequiresOptIn',
        '-Xopt-in=androidx.compose.runtime.ExperimentalComposeApi',
      ]
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach {
    kotlinOptions {
      jvmTarget = '1.8'
      useIR = true
    }
  }

  plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
    def android = plugin.extension
    android.compileSdkVersion 30
    android.defaultConfig {
      minSdkVersion 21
      targetSdkVersion 30
    }
    android.lintOptions {
      checkDependencies true
      checkReleaseBuilds false // Full lint runs as part of 'build' task.
    }
  }
}
