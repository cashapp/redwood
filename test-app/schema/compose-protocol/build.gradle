import app.cash.redwood.buildsupport.KmpTargets

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'app.cash.redwood.generator.compose.protocol'

kotlin {
  KmpTargets.addAllTargets(project)

  // Ensure our codegen correctly works in explicit API mode.
  explicitApi()

  sourceSets {
    commonMain {
      dependencies {
        api projects.testApp.schema.compose
      }
    }
  }

  targets.all {
    compilations.all {
      kotlinOptions {
        // Ensure our codegen does not produce any warnings.
        allWarningsAsErrors = true

        freeCompilerArgs += [
          // Ensure our codegen can handle the latest language semantics.
          // https://kotlinlang.org/docs/compiler-reference.html#progressive
          '-progressive'
        ]
      }
    }
  }
}

redwoodSchema {
  source = projects.testApp.schema
  type = 'example.redwood.TestSchema'
}
