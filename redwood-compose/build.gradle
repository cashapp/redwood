import app.cash.redwood.buildsupport.JsTests

import static app.cash.redwood.buildsupport.TargetGroup.CommonWithAndroid

redwoodBuild {
  targets(CommonWithAndroid + JsTests.Browser)
  publishing()
}

apply plugin: 'org.jetbrains.kotlin.plugin.compose'

kotlin {
  applyDefaultHierarchyTemplate {
    it.group("common") {
      it.group("nonJs") {
        it.group("ios") {}
        it.withAndroidTarget()
        it.withJvm()
      }
    }
  }

  sourceSets {
    commonMain {
      dependencies {
        api libs.kotlinx.coroutines.core
        api projects.redwoodRuntime
        api projects.redwoodWidget
        api libs.jetbrains.compose.runtime
        api libs.jetbrains.compose.runtime.saveable
      }
    }
    commonTest {
      dependencies {
        implementation libs.kotlin.test
        implementation libs.kotlinx.coroutines.test
        implementation libs.assertk
        implementation projects.testApp.schema.testing
        implementation projects.testApp.schema.compose
        implementation projects.testApp.schema.protocolGuest
        implementation projects.testApp.schema.protocolHost
      }
    }
    androidMain {
      dependencies {
        implementation libs.androidx.core
      }
    }
    nonJsMain {
      kotlin.srcDir(redwoodBuild.generateComposeHelpers(tasks, 'app.cash.redwood.compose'))
      dependencies {
        implementation libs.jetbrains.compose.collection
      }
    }
  }
}

dependencies {
  androidTestImplementation libs.junit
  androidTestImplementation libs.androidx.test.runner
  androidTestImplementation libs.kotlinx.coroutines.test
  androidTestImplementation libs.assertk
}

android {
  namespace 'app.cash.redwood.compose'

  defaultConfig {
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }
}

spotless {
  kotlin {
    targetExclude(
      // Apache 2-licensed files from AOSP.
      "src/androidMain/kotlin/app/cash/redwood/compose/AndroidUiDispatcher.kt",
      "src/androidMain/kotlin/app/cash/redwood/compose/AndroidUiFrameClock.kt",
    )
  }
}
