import app.cash.redwood.buildsupport.ComposeHelpers
import app.cash.redwood.buildsupport.KmpTargets

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'

redwoodBuild {
  composeCompiler()
  publishing()
}

kotlin {
  KmpTargets.addAllTargets(project)

  sourceSets {
    commonMain {
      kotlin.srcDir(ComposeHelpers.get(tasks, 'app.cash.redwood.compose'))
      dependencies {
        api libs.kotlinx.coroutines.core
        api projects.redwoodRuntime
        api projects.redwoodWidget
        api libs.jetbrains.compose.runtime
      }
    }
    commonTest {
      dependencies {
        implementation libs.kotlin.test
        implementation libs.kotlinx.coroutines.test
        implementation libs.assertk
        implementation projects.testApp.schema.testing
        implementation projects.testApp.schema.compose
        implementation projects.testApp.schema.composeProtocol
        implementation projects.testApp.schema.widgetProtocol
      }
    }
    androidMain {
      dependencies {
        implementation libs.androidx.core
      }
    }
  }
}

dependencies {
  androidTestImplementation libs.junit
  androidTestImplementation libs.androidx.test.runner
  androidTestImplementation libs.kotlinx.coroutines.test
  androidTestImplementation libs.assertk
}

android {
  namespace 'app.cash.redwood.compose'

  defaultConfig {
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }
}

spotless {
  kotlin {
    targetExclude(
      // Apache 2-licensed files from AOSP.
      "src/androidMain/kotlin/app/cash/redwood/compose/AndroidUiDispatcher.kt",
      "src/androidMain/kotlin/app/cash/redwood/compose/AndroidUiFrameClock.kt",
    )
  }
}
