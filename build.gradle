buildscript {
  ext.versions = [
    'kotlin': '1.4.31',
    'kotlinx': [
      'coroutines': '1.4.3',
      'serialization': '1.0.0',
    ],
    'exhaustive': '0.1.1',
  ]

  ext.deps = [
    'kotlin': [
      'compiler': "org.jetbrains.kotlin:kotlin-compiler:${versions.kotlin}",
      'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
      'reflect': 'org.jetbrains.kotlin:kotlin-reflect',
      'test': 'org.jetbrains.kotlin:kotlin-test-multiplatform',
    ],
    'kotlinx': [
      'coroutines': [
        'android': "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlinx.coroutines}",
        'core': "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinx.coroutines}",
      ],
      'serialization': [
        'core': "org.jetbrains.kotlinx:kotlinx-serialization-core:${versions.kotlinx.serialization}",
      ],
    ],
    'androidx': [
      'core': 'androidx.core:core-ktx:1.2.0',
    ],
    'kotlinPoet': 'com.squareup:kotlinpoet:1.7.2',
    'clikt': 'com.github.ajalt.clikt:clikt:3.1.0',
    'exhaustiveAnnotation': "app.cash.exhaustive:exhaustive-annotation:${versions.exhaustive}",
    'junit': 'junit:junit:4.13.1',
    'testParameterInjector': 'com.google.testparameterinjector:test-parameter-injector:1.1',
    'truth': 'com.google.truth:truth:1.1',
  ]

  dependencies {
    classpath deps.kotlin.gradlePlugin
    classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
    classpath 'com.android.tools.build:gradle:4.0.2'
    classpath "app.cash.exhaustive:exhaustive-gradle:${versions.exhaustive}"
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.14.2'
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.4.30'
    classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    classpath 'com.diffplug.spotless:spotless-plugin-gradle:5.8.2'
  }
  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

apply plugin: 'org.jetbrains.dokka'

allprojects {
  repositories {
    mavenCentral()
    google()
    maven {
      url 'https://kotlin.bintray.com/kotlinx'
    }
    jcenter()
  }

  tasks.withType(Test).configureEach {
    testLogging {
      if (System.getenv("CI") == "true") {
        events = ["failed", "skipped", "passed"]
      }
      exceptionFormat "full"
    }
  }

  plugins.withId('org.jetbrains.kotlin.multiplatform') {
    kotlin.sourceSets.all {
      languageSettings {
        useExperimentalAnnotation('kotlin.RequiresOptIn')
        useExperimentalAnnotation('androidx.compose.runtime.ExperimentalComposeApi')
      }
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
    task.kotlinOptions {
      jvmTarget = '1.8'
      useIR = true
      freeCompilerArgs += [
        '-Xopt-in=kotlin.RequiresOptIn',
        '-Xopt-in=androidx.compose.runtime.ExperimentalComposeApi',
      ]
    }
  }

  // This is required for JetBrains' Compose fork to build JS correctly.
  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJsCompile).configureEach { task ->
    task.kotlinOptions {
      freeCompilerArgs += [
        "-P", "plugin:androidx.compose.compiler.plugins.kotlin:generateDecoys=true",
      ]
    }
  }

  plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
    def android = plugin.extension
    android.compileSdkVersion 30
    android.defaultConfig {
      minSdkVersion 21
      targetSdkVersion 30
    }
    android.lintOptions {
      checkDependencies true
      checkReleaseBuilds false // Full lint runs as part of 'build' task.
    }
  }

  plugins.withId('com.vanniktech.maven.publish') {
    mavenPublish {
      targets {
        installArchives {
          def url = file("${rootProject.buildDir}/localMaven").toURI().toString()
          releaseRepositoryUrl = url
          snapshotRepositoryUrl = url
        }
      }
    }
  }

  if (!project.path.startsWith(':compose:')) {
    apply plugin: 'com.diffplug.spotless'

    spotless {
      kotlin {
        target("src/**/*.kt")
        // Spotless doesn't read .editorconfig yet: https://github.com/diffplug/spotless/issues/142
        ktlint('0.41.0').userData([
          'insert_final_newline': 'true',
          'end_of_line': 'lf',
          'charset': 'utf-8',
          'indent_size': '2',
        ])
      }
    }
  }
}
